/// matches whitespace
WHITESPACE = _{ (" " | "\t")+ }

/// allows for comments
COMMENT = _{ "//" ~ (!NEWLINE ~ ANY)* ~ (NEWLINE | EOI) }

/// matches x coordinate
x = { "x" }

/// matches y coordinate
y = { "y" }

/// matches z coordinate
z = { "z" }

/// matches a sign
sign = { "+" | "-" }

/// matches non-negative integers
integer = ${ ASCII_DIGIT+ }

/// any nonzero digit
nz_digit = { '1' .. '9' }

/// any version of zero
num_zero = ${ "0"+ ~ !nz_digit }

/// matches valid positive rantional numbers
p_rational_num = {
    (integer ~ "/" ~ !num_zero ~ integer | integer ~ !"/")
}

/// matches the operation on one coefficient
coeff_op = {
    sign? ~ x ~ (sign ~ y)? ~ (sign ~ z)? ~ (sign ~ p_rational_num)?
  | sign? ~ y ~ (sign ~ z)? ~ (sign ~ p_rational_num)?
  | sign? ~ z ~ (sign ~ p_rational_num)?
}

/// matches the operation
operation = { (coeff_op ~ ","){2} ~ coeff_op }

/// matches any rational number
coeff_translation = { sign? ~ p_rational_num }

/// matches a translation operation
translation_op = { (coeff_translation ~ ","){2} ~ coeff_translation }

/// matches a list of operations
op_list = { ((operation | translation_op) ~ ";" ~ NEWLINE?)+ }
